allprojects {
    buildscript {
        repositories {
//            jcenter()
            maven {
                credentials {
                    username gbArtifactoryUser
                    password gbArtifactoryPassword
                }
                url gbArtifactoryUrl
            }
        }
    }
    repositories {
//        jcenter()
        maven {
            credentials {
                username gbArtifactoryUser
                password gbArtifactoryPassword
            }
            url gbArtifactoryUrl
        }
    }

    //cross-compilation stuff for Java 7
    assert (System.env.JDK_7_HOME != null): "Set environment property JDK_7_HOME"
    project.ext.targetJavaHome = System.env.JDK_7_HOME
    def javaExecutablesPath = new File(targetJavaHome, 'bin')
    def javaExecutables = [:].withDefault { execName ->
        def executable = new File(javaExecutablesPath, execName)
        assert executable.exists(): "There is no ${execName} executable in ${javaExecutablesPath}"
        executable
    }
    tasks.withType(JavaCompile) {
        project.sourceCompatibility = '1.7'
        options.with {
            fork = true
            forkOptions.javaHome = file(targetJavaHome)
        }
    }
    tasks.withType(Javadoc) {
        executable = javaExecutables.javadoc
    }
    tasks.withType(Test) {
        executable = javaExecutables.java
    }
    tasks.withType(JavaExec) {
        executable = javaExecutables.java
    }
}

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        maven {
            credentials {
                username gbArtifactoryUser
                password gbArtifactoryPassword
            }
            url gbArtifactoryUrl
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

ext.with {
    BatmDependencySubstitutionConfig = file('dependencySubstitutions.txt')
}